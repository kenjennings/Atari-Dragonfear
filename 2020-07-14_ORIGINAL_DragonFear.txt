5 GRAPHICS 1+16:PRINT #6;"PLEASE WAIT AS THE  WORLD IS BUILT":GOSUB 2000
80 REM **** DRAW SCREEN ****
90 GOSUB 1000
130 REM **** MOVE PLAYER ****
140 PRINT "YOU HAVE ";40-H;" MOVES REMAINING."
150 PRINT "WHAT DO YOU DO";:INPUT Z$
160 IF Z$<>"N" AND Z$<>"S" AND Z$<>"E" AND Z$<>"W" AND Z$<>"F" AND Z$<>"Q" THEN 130
165 IF Z$="N" AND A(E-20)=88 OR Z$="S" AND A(E+20)=88 THEN PRINT "YOUR WAY IS BLOCKED!":GOTO 130
166 IF Z$="E" AND A(E+1)=88 OR Z$="W" AND A(E-1)=88 THEN PRINT "YOUR WAY IS BLOCKED!":GOTO 130
170 A(E)=32:IF Z$="N" THEN E=E-20
180 IF Z$="S" THEN E=E+20
190 IF Z$="E" THEN E=E+1
200 IF Z$="W" THEN E=E-1
210 IF A(E)=113 THEN GOSUB 500
220 IF A(E)=63 THEN GOSUB 400
230 IF A(E)=100 THEN GOSUB 300
240 IF A(E)=36 THEN GOSUB 600
298 H=H+1
299 GOSUB 1000:GOTO 130
300 REM **** DRAGON ****
310 PRINT "OH NO A DRAGON!!!!":GOSUB 700
320 ON R GOTO 330,350,370,380
330 PRINT "YOU WOKE A SLEEPING DRAGON.":PRINT "HE TURNS AND BREATHS A BLAST OF FIRE."
340 PRINT "YOU ARE BURNED TO ASH!":GOTO 800
350 PRINT "IT IS SLEEPING AND IT":PRINT "DOESN'T DETECT YOU. YOU RAID IT'S TREASURE.":GOSUB 600:GOTO 399
370 PRINT "YOU SURPRISE THE DRAGON. A FIGHT BEGINS!":GOTO 399
380 PRINT "YOU SURPRISE THE DRAGON AND IT QUICKLY ATTACKS.":PRINT "A FIGHT BEGINS.":GOTO 399
399 GOSUB 1070:RETURN 
400 REM **** MAGIC ****
410 PRINT :PRINT "YOU ARE SUDDENLY ENGULFED IN A BRIGHT LIGHT!"
420 PRINT "YOU STUMBLED INTO A MAGIC CAVE."
440 GOSUB 1070
450 A(E)=88
460 E=INT(RND(0)*276)+12:IF A(E)=88 THEN 460
470 RETURN 
500 REM **** QUICKSAND ****
510 PRINT "OH NO QUICKSAND!!!"
599 GOSUB 1070:RETURN 
600 REM **** TREASURE ****
610 K=INT(RND(0)*100)+100
620 PRINT "YOU'VE FOUND ";K;:PRINT " GOLD PIECES."
630 G=G+K:PRINT "YOU HAVE ";G;" GOLD PEICES."
699 GOSUB 1070:RETURN 
700 REM **** RANDOM RESULTS ****
710 R=INT(RND(0)*4)+1
720 RETURN 
800 REM **** GAME OVER ****
810 FOR D=1 TO 1000:NEXT D
820 PRINT "YOU HAVE DIED!. YOU FOUND ";K;" GOLD PEICES.":GOSUB 1070:GRAPHICS 0:END 
1000 REM ***********
1001 REM DRAW SCREEN
1002 REM **********
1005 GRAPHICS 1
1010 PRINT #6;" LAND OF DRAGONFEAR"
1020 A(E)=72
1030 FOR J=1 TO 300
1040 PRINT #6;CHR$(A(J));
1050 IF 20*INT(J/20)=J THEN PRINT #6;
1060 NEXT J:RETURN 
1070 FOR O=1 TO 2500:NEXT O:RETURN 
2000 REM **** BUILD THE WORLD ****
2010 DIM A(300),B(25),C(8),Z$(1):H=0:Q=0:L=0:G=0:AR=6
2020 FOR B=1 TO 300:A(B)=32
2030 IF B<22 OR B>280 OR 20*INT(B/20)=B OR 20*INT(B/20)=B-1 THEN A(B)=88
2040 NEXT B
2050 FOR B=1 TO 25
2055 Z=INT(RND(0)*276)+12:IF A(Z)=88 THEN 2055
2060 READ C:A(Z)=C:NEXT B
2080 E=110:RETURN 
2090 DATA 88,63,100,113,36,88,63,100,113,36,88,63,100,113,36,88,63,100,113,36,88,63,100,113,36
